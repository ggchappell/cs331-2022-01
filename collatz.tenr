#!./tenrec.lua
# collatz.tenr
# Glenn G. Chappell
# 2022-04-01
#
# For CS F331 / CSCE A331 Spring 2022
# Collatz Sequences in Tenrec


# Function collatz
# Given a positive integer k, applies the Collatz function once and
# returns the result. k is not modified.
func collatz()
{
    if (k % 2 == 0)
    {
        return k/2;
    }
    else
    {
        return 3*k + 1;
    }
}


# Function printCollatzSequence
# Given a positive integer n, print the Collatz sequence beginning at n
# and ending at 1, items separated by blanks and ending with a newline.
# Modifies variable k.
func printCollatzSequence()
{
    k = n;
    while (k != 1)
    {
        print(k, " ");
        k = collatz();
    }
    print(k, cr);
}


# Main Program
# Repeatedly input a number and prints its Collatz sequence. End by
# entering zero or a blank line.

# Print header
print("Tenrec - Collatz Sequences Demo", cr, cr);

# Input & sequence printing
done = false;
while (not done)
{
    print("Type a number (0 or blank line to end): ");
    n = read();
    if (n <= 0)
    {
        done = true;
    }
    else
    {
        print(cr);
        print("Collatz sequence for ", n, ":", cr);
        printCollatzSequence();
        print(cr);
    }
}

# Wait for user
# (unnecessary with tenrec.lua, so commented out)
#print(cr, "Press ENTER to quit ");
#dummy = read();

